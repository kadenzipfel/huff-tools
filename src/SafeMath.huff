// Interface
#define function safeAdd(uint256,uint256) pure returns (uint256)
#define function safeSub(uint256,uint256) pure returns (uint256)

// Methods - Use these macros directly
#define macro SAFE_ADD() = takes(2) returns (1) {
    // input stack          // [num1, num2]
    dup2                    // [num2, num1, num2]
    add                     // [result, num2]
    dup1                    // [result, result, num2]
    swap2                   // [num2, result, result]
    gt                      // [is_overflow, result]
    iszero                  // [is_not_overflow, result]
    is_not_overflow jumpi   // [result]
        0x00 0x00 revert
    is_not_overflow:        // [result] 
}

// Wrapper methods (for testing)
#define macro SAFE_ADD_WRAPPER() = takes (0) returns (0) {
    0x04 calldataload       // [num1]
    0x24 calldataload       // [num2, num1]
    SAFE_ADD()              // [result]
    0x00 mstore             // []
    0x20 0x00 return        // []
}

#define macro SAFE_SUB_WRAPPER() = takes (0) returns (0) {
    0x24 calldataload       // [num2]
    0x04 calldataload       // [num1, num2]
    dup1                    // [num1, num1, num2]
    dup3                    // [num2, num1, num1, num2]
    gt                      // [is_underflow, num1, num2]
    is_underflow jumpi      // [num1, num2]

    sub                     // [result]
    0x00 mstore             // []
    0x20 0x00 return        // []

    is_underflow:
        pop                 // [num2]
        pop                 // []
        0x00 0x00 revert    // []
}

// Main
#define macro MAIN() = takes (0) returns (0) {
    0x00 calldataload 0xE0 shr
    dup1 0xe6cb9013 eq safeAdd jumpi
    dup1 0xa293d1e8 eq safeSub jumpi

    safeAdd: 
        SAFE_ADD_WRAPPER()
    safeSub: 
        SAFE_SUB_WRAPPER()
}